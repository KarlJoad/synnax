(use-modules (gnu)
             (ice-9 format))
(use-service-modules networking ssh web cuirass)
(use-package-modules bootloaders ssh version-control certs)

;; NOTE: If using SSH-protected channels, MUST have nss-certs in globally
;; available packages!
(define %cuirass-specification
  #~(list (specification
           (name "personal")
           (channels (list (channel
                            (name 'synnax)
                            (url "https://github.com/KarlJoad/synnax.git"))
                           (channel
                            (name 'nonguix)
                            (url "https://gitlab.com/nonguix/nonguix"))
                           (channel (inherit %default-guix-channel))))
           (build '(channels synnax))
           ;; How frequently to attempt to build this specification, in seconds
           (period 0))
          (specification
           (name "nonguix")
           (channels (list
                      (channel
                       (name 'nonguix)
                       (url "https://gitlab.com/nonguix/nonguix"))
                      (channel (inherit %default-guix-channel))))
           (build '(channels nonguix)))
    ))


(define (ci-system host-name
                   bootloader-target
                   swap-uuid
                   root-fs-uuid
                   authorized-guix-signing-keys)
  (operating-system
   (locale "en_US.utf8")
   (host-name host-name)
   (timezone "America/Chicago")
   (bootloader
    (bootloader-configuration
     (bootloader grub-bootloader)
     (targets (list bootloader-target))
     (keyboard-layout (keyboard-layout "us"))
     (terminal-outputs '(console))))
   (swap-devices
    (list (swap-space (target (uuid swap-uuid)))))
   (file-systems
    (cons* (file-system
            (mount-point "/")
            (device (uuid root-fs-uuid 'ext4))
            (type "ext4"))
           %base-file-systems))
   (packages (append (list git nss-certs) %base-packages))
   (services
    (append (list (service dhcp-client-service-type)
                  (simple-service 'add-dhclient-conf etc-service-type
                                  (list `("dhclient.conf"
                                          ,(plain-file "dhclient.conf"
                                                       (format #f "send host-name \"~a\";" host-name)))))
                  (service openssh-service-type
                           (openssh-configuration
                            (openssh openssh-sans-x)
                            (password-authentication? #false)
                            (permit-root-login #t)
                            (log-level 'debug)
                            (authorized-keys
                             ;; Authorise our SSH key.
                             ;; SSH access must be able to access/elevate to user in config list at bottom
                             `(("root" ,(local-file "./ci_rsa.pub"))))))
                  (service cuirass-service-type
                           (cuirass-configuration
                            (specifications %cuirass-specification)
                            ;; How frequently to fetch spec's channels, in seconds
                            (interval (* 1 60 60))
                            (host "0.0.0.0"))))
            (modify-services %base-services
                             (guix-service-type config =>
                                                (guix-configuration
                                                 (inherit config)
                                                 ;; (extra-options (list "--max-jobs" 4
                                                 ;;           "--cores" 4))
                                                 (authorized-keys
                                                  ;; Guix signing key generated by Guix in /etc/guix/
                                                  (append authorized-guix-signing-keys
                                                          %default-authorized-guix-keys)))))))))

(define white-ci-system
  (ci-system "Karl-CI"
             "/dev/sda"
             "dacd0179-d888-47d4-a910-ac58ae14fac3"
             "9f14407e-8cb3-4b09-b2c3-3363340fafdd"
             (list
              (local-file "./desktop-guix-signing-key.pub")
              (local-file "./guix-coordinator.pub"))))

(list (machine
       (operating-system white-ci-system)
       (environment managed-host-environment-type)
       (configuration (machine-ssh-configuration
           ;; IP or DNS-resolved address of machine(s) to manage
           (host-name "Karl-CI.raven")
           (system "x86_64-linux")
           ;; SSH host key of system being configured
           (host-key "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIG2OIXsMCJ3SxJcQTZj4B7OVc2uD4K3bd56ST8GJyi1p root@(none)")
           (user "root")
           ;; SSH identity key for machine deploying to connect to remote
           (identity "~/.ssh/ci_rsa")))))
